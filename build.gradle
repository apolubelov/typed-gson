
plugins {
    id 'scala'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'com.github.apolubelov.general'
version '1.0.0'

sourceCompatibility = 1.8
compileScala.targetCompatibility = 1.8

dependencies {
    compile 'org.scala-lang:scala-library:2.12.0'
    compile('com.google.code.gson:gson:2.7')

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    // java chain code uses different logger facades, namely JUL and commons logging ...
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'

    // testing libraries
    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testCompile 'org.mockito:mockito-core:2.23.0'

    testRuntime 'org.scala-lang.modules:scala-xml_2.12:1.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: [javadoc, scaladoc]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId group
            artifactId rootProject.name
            version version
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    pkg {
        repo = 'general'
        name = 'typed-gson'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/apolubelov/typed-gson'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}